package require tcltest

tcltest::test package-1.1 {package load} {
    lappend auto_path .
    package require oowidgets
} {0.4.0}

tcltest::test flash-1.1 {new button} {
    namespace eval ::test { }
    oowidgets::widget ::test::Button {
       constructor {path args} {
          my install ttk::button $path -message testmessage \
            -flashtime 100
          my configure {*}$args
       }
       method test {} {
           return [my cget -message]
       }
   }  
   info commands ::test::*
} {::test::Button ::test::button}

tcltest::test flash-1.2 {flash button} {
    set btn [::test::button .btn -command exit -text Exit]
    pack $btn -side top -padx 10 -pady 10 -ipadx 10 -ipady 10
    $btn test
} {testmessage}

tcltest::test flash-1.3 {update message with widgetpath} {
    .btn configure -message newmessage
    .btn test
} {newmessage}

tcltest::test flash-1.4 {update message with variable} {
    $btn configure -message btnmessage
    $btn test
} {btnmessage}

tcltest::test flash-1.5 {get flashtime} {
    $btn cget -flashtime
} {100}

tcltest::test flash-1.6 {create flash method} {
    oo::define ::test::Button method flash {} {
        set otext [my cget -text]
        update idletasks
        after 300 
        my configure -text "...."
        update idletasks
        after 300
        my configure -text $otext
        update idletasks
        after 300
        return "flashing [my cget -flashtime] millisecs"
    }
    $btn configure -flashtime 200
    $btn flash
} {flashing 200 millisecs}


tcltest::test exit-1.1 {exit test application} {
    destroy [winfo children .]
} {}
